//noinspection GradleCompatible
apply plugin: 'com.android.application'

def compile_sdk_version = 28
def min_sdk_version = 21
def support_library_version = "27.1.1"
def androidX = "1.0.0-rc02"
def build_tools_version = "28.0.2"
def play_services_version = "15.0.0"
def firebase_version = "15.0.0"
def support_test_version = "1.0.1"
def gson_version = "2.8.2"
def glide_version = "3.8.0"



configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        // Skip multidex because it follows a different versioning pattern.
        if (!details.requested.name.startsWith("multidex")) {
            switch (details.requested.group) {
                case 'com.android.support':
                    details.useVersion "$support_library_version"
                    break
                case 'com.google.android.gms':
                    details.useVersion "$play_services_version"
                    break
                case 'com.google.firebase':
                    details.useVersion "$firebase_version"
                    break
                case 'com.android.support.test':
                    details.useVersion "$support_test_version"
                    break
                case 'com.google.code.gson':
                    details.useVersion "$gson_version"
                    break
                case 'com.github.bumptech.glide':
                    details.useVersion "$glide_version"
                    break
            }
        }
    }
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version
    defaultConfig {
        applicationId "com.android.mycax.floatingvolume"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode 12
        versionName "1.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.material:material:${androidX}"
    implementation "androidx.appcompat:appcompat:${androidX}"
    implementation "androidx.recyclerview:recyclerview:${androidX}"
    implementation "androidx.cardview:cardview:${androidX}"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${support_test_version}"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.github.recruit-lifestyle:FloatingView:2.3.2'
    implementation 'com.github.jrvansuita:MaterialAbout:0.2.3'
    implementation 'com.karumi:dexter:5.0.0'
    implementation "com.mikepenz:aboutlibraries:6.2.0-rc01"
    implementation 'com.thebluealliance:spectrum:0.7.1'
    implementation 'com.github.medyo:fancybuttons:1.9.0'
}